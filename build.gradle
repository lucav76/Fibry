plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    //id 'org.kordamp.gradle.java-project' version '0.45.0'
    id 'org.jreleaser' version '1.14.0'
}

group 'eu.lucaventuri'
version version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation 'org.reactivestreams:reactive-streams-tck-flow:1.0.4'
    testImplementation group: 'org.testng', name: 'testng', version: '6.14.3'

    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.3'
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.0-rc1'
}

apply from: 'publish.gradle'

task testNG(type: Test) { useTestNG() }
test.dependsOn testNG

class ReleaseCheckState {
    static volatile String releaseNotes = ""
}

task testJar(type: Jar) {
    from sourceSets.test.output + sourceSets.main.output
}

tasks.register("releaseCheck") {
    if (gradle.startParameter.taskNames.any { it == "release" || it == "jreleaserConfig" || it == "jreleaserFullRelease" }) {
        ReleaseCheckState.releaseNotes = file("RELEASE_NOTES.md").text

        println "*** RELEASE NOTES:\n$ReleaseCheckState.releaseNotes\n"

        ant.input(message: 'Are the Release Notes correct?', addproperty: 'releaseOk', defaultValue: 'N')

        if (!"y".equalsIgnoreCase(ant.properties.releaseOk.toString()))
            System.exit(0)
    }
}

jreleaser {
    release {
        github {
            overwrite = true
            changelog {
                external = file('RELEASE_NOTES.md')
            }
        }
    }
}